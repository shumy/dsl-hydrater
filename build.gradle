plugins {
  id 'org.jetbrains.kotlin.jvm' version '1.4.20'
  id 'com.adarshr.test-logger' version '2.1.1'
  id 'jacoco'
}

allprojects {
  apply plugin: 'org.jetbrains.kotlin.jvm'
  apply plugin: 'com.adarshr.test-logger'
  apply plugin: 'jacoco'
  
  group 'net.dsl-hydrater'
  version '0.1.0'
  
  repositories {
    jcenter()
  }

  dependencies {
    implementation platform('org.jetbrains.kotlin:kotlin-bom')
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    implementation 'org.slf4j:slf4j-simple:1.7.30'

    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit'
  }
  
  test {
    dependsOn cleanTest
    finalizedBy jacocoTestReport
  }

  jacoco {
    reportsDirectory = file("${project.projectDir}/jacoco")
  }

  jacocoTestReport {
    afterEvaluate {
      classDirectories.setFrom(files(classDirectories.files.collect {
        fileTree(dir: it, exclude: 'hyd/dsl/antlr/**')
      }))
    }
  }

  jar {
    manifest {
      attributes 'Implementation-Title': project.name,
        'Implementation-Version': archiveVersion,
        'Built-By': 'micaelpedrosa@gmail.com'
    }
  }

  clean {
    delete "${project.projectDir}/jacoco"
  }
}