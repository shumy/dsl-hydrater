plugins {
  id 'antlr'
}

sourceSets {
  main.java.srcDirs += 'gen-src'
}

/**
 * Without the next section Gradle will add a 'compile' dependency on Antlr3:
 * https://github.com/gradle/gradle/issues/820
 */
configurations {
  implementation {
    extendsFrom = extendsFrom.findAll { it != configurations.antlr }
  }
}

dependencies {
  antlr 'org.antlr:antlr4:4.8'

  implementation project(':')
  implementation 'org.antlr:antlr4-runtime:4.8'
  implementation 'org.jetbrains.kotlin:kotlin-reflect'
}

generateGrammarSource {
  maxHeapSize = '64m'
  outputDirectory = file('gen-src')
}

compileKotlin {
  dependsOn generateGrammarSource
  kotlinOptions {
    freeCompilerArgs += '-Xuse-experimental=kotlin.ExperimentalStdlibApi'
  }
}

clean.doLast {
  file('gen-src').deleteDir()
}